# Build the pinned deepseek-harness runtime from the npm release recorded in
# this repository's dsh-source.json. The npm package ships compiled `lib/`
# and `config/`, so only the dependency graph needs installation.

{ lib
, stdenv
, fetchPnpmDeps
, fetchurl
, nodejs_24
, pnpm
, pnpmConfigHook
, runCommand
, dshSourceSpec
}:

assert dshSourceSpec.source == "npm";

let
  tarball = fetchurl {
    url = dshSourceSpec.tarball;
    hash = dshSourceSpec.integrity;
  };

  # pnpm install needs the lockfile and supply-chain policy beside
  # package.json; the npm tarball carries neither repository file.
  src = runCommand "dsh-runtime-pinned-src" { } ''
    mkdir -p $out
    tar -xzf ${tarball} -C $out --strip-components=1
    cp ${../.npmrc} $out/.npmrc
    cp ${../scripts}/dsh-runtime-${dshSourceSpec.version}-lock.yaml $out/pnpm-lock.yaml
    printf '%s\n' \
      'packages:' \
      '  - .' \
      "" \
      'minimumReleaseAgeExclude:' \
      "  - '@deepseek-ai/*'" \
      > $out/pnpm-workspace.yaml
  '';
in

stdenv.mkDerivation rec {
  pname = "dsh-runtime-pinned";
  version = dshSourceSpec.version;

  inherit src;

  pnpmDeps = fetchPnpmDeps {
    inherit pname version src;
    fetcherVersion = 4;
    hash = "sha256-cjSI0PFYvpUGJZEfuuL6/4JCvK4V+yf/psEAfrZ9FRQ=";
  };

  nativeBuildInputs = [ nodejs_24 pnpm pnpmConfigHook ];

  buildPhase = ''
    runHook preBuild
    pnpm install --frozen-lockfile --ignore-scripts
    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall
    mkdir -p $out/lib/dsh
    cp -r lib config package.json node_modules $out/lib/dsh/
    runHook postInstall
  '';

  meta = with lib; {
    description = "Pinned DeepSeek Harness npm runtime (${dshSourceSpec.version})";
    license = licenses.mit;
    platforms = platforms.unix;
  };
}
